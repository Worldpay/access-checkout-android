apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

dokkaJavadoc.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            outputDirectory.set(layout.buildDirectory.resolve("javadoc"))
            skipEmptyPackages.set(true)
            includeNonPublic.set(false)
        }
    }
}

// Task dependency is required due to an issue in Dokka
// see https://github.com/Kotlin/dokka/issues/3153
dokkaJavadoc.dependsOn(':access-checkout:generateReleaseRFile')

tasks.register('javadocJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier = "javadoc"
    from "${layout.buildDirectory.dir('.').get().asFile}/dokka/javadoc"
    archiveAppendix = "android"
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = "sources"
    from android.sourceSets.main.java.srcDirs
    archiveAppendix = "android"
}

artifacts {
    archives javadocJar, sourcesJar
}

android {
    publishing {
        singleVariant('release') {

        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocJar

                groupId = "com.worldpay.access"
                artifactId = "${project.archivesBaseName}"
                version = "${project.version}"

                pom {
                    name = 'Worldpay Access Checkout Android SDK'
                    packaging = 'aar'

                    description = 'Android SDK library for Worldpay Access Checkout.'
                    url = 'https://github.com/Worldpay/access-checkout-android'

                    scm {
                        connection = 'scm:git:git://github.com/Worldpay/access-checkout-android.git'
                        developerConnection = 'scm:git:ssh://github.com/Worldpay/access-checkout-android.git'
                        url = 'https://github.com/Worldpay/access-checkout-android/tree/master'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://raw.githubusercontent.com/Worldpay/access-checkout-android/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'worldpay'
                            name = 'Worldpay'
                            organization = 'Worldpay'
                            organizationUrl = 'https://www.worldpay.com/'
                        }
                    }
                }
            }
        }
    }

    if (project.hasProperty("signing.keyId")) {
        signing {
            required { true }
            sign publishing.publications.release
        }
    }
}

