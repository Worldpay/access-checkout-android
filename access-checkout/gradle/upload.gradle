apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

dokkaJavadoc.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            outputDirectory.set(buildDir.resolve("javadoc"))
            skipEmptyPackages.set(true)
            includeNonPublic.set(false)
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier "javadoc"
    from "$buildDir/dokka/javadoc"
    appendix "android"
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
    appendix "android"
}

artifacts {
    archives javadocJar, sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocJar

                groupId = "com.worldpay.access"
                artifactId = "${project.archivesBaseName}"
                version = "${project.version}"

                pom {
                    name = 'Worldpay Access Checkout Android SDK'
                    packaging = 'aar'

                    description = 'Android SDK library for Worldpay Access Checkout.'
                    url = 'https://github.com/Worldpay/access-checkout'

                    scm {
                        connection = 'scm:git:git://github.com/Worldpay/access-checkout.git'
                        developerConnection = 'scm:git:ssh://github.com/Worldpay/access-checkout.git'
                        url = 'https://github.com/Worldpay/access-checkout/tree/master'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://raw.githubusercontent.com/Worldpay/access-checkout/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'worldpay'
                            name = 'Worldpay'
                            organization = 'Worldpay'
                            organizationUrl = 'https://www.worldpay.com/'
                        }
                    }
                }
            }
        }
    }

    if (project.hasProperty("signing.keyId")) {
        signing {
            required { true }
            sign publishing.publications.release
        }
    }
}

