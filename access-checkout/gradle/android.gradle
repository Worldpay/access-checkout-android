android {
    compileSdk = 35

    namespace = "com.worldpay.access.checkout"

    buildFeatures {
        buildConfig = true
    }

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
        debug {
            // The test coverage has been disabled here due to a bug in the android build tools (7.0.2)
            // setting this to false changes the location of where the exec file lives for jacoco to
            // build/jacoco/testDebugUnitTest.exec with the correct coverage information.
            // If this is set to true, then the location of the execution data file is at
            // ./build/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec
            // and the coverage information in that file is incorrect and reports 0% code coverage.
            //
            // for more information see: https://issuetracker.google.com/issues/195860510
            testCoverageEnabled = true
            debuggable = true
            minifyEnabled = false
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all { output ->
            def name = "${archivesBaseName}-${version}"
            if (output.outputFileName.endsWith('.aar')) {
                if (variant.name == "release") {
                    output.outputFileName = "${name}.aar"
                }
                if (variant.name == "debug") {
                    output.outputFileName = "${name}-debug.aar"
                }
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

}
