apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.10'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        '**/logging/LoggingUtils*',
        '**/logging/AccessCheckoutLogger*',
        // excludes classes generated by for coroutines by compiler, e.g. see method doGet in HttpsClient
        '**/HttpsClient*$2*',
        '**/*.<init>(*DefaultConstructorMarker*)'
]

def classDirs = files([
        fileTree(dir: "${layout.buildDirectory.dir('.').get().asFile}/intermediates/classes/debug", excludes: fileFilter) +
                fileTree(dir: "${layout.buildDirectory.dir('.').get().asFile}/tmp/kotlin-classes/debug", excludes: fileFilter)
])

def execData = files(
        // Unit test execution data
        "${layout.buildDirectory.dir('.').get().asFile}/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
        // Android instrumented tests execution data
        fileTree(
                dir: "${layout.buildDirectory.dir('.').get().asFile}/outputs/code_coverage/debugAndroidTest/connected/",
                includes: ["**/*.ec"]
        )
)

def sourceDirs = files(["${project.projectDir}/src/main/java"])

tasks.register('jacocoTestReport', JacocoReport) {
    executionData.setFrom(execData)
    classDirectories.setFrom(classDirs)
    sourceDirectories.setFrom(sourceDirs)
}
tasks.named('jacocoTestReport') {
    dependsOn('connectedDebugAndroidTest')
    dependsOn('testDebugUnitTest')
}

tasks.register('jacocoTestCoverageVerification', JacocoCoverageVerification) {
    dependsOn 'jacocoTestReport'
    executionData.setFrom(execData)
    classDirectories.setFrom(classDirs)
    sourceDirectories.setFrom(sourceDirs)

    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }
        }
    }
}
