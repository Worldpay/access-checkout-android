apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.10'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        '**/logging/LoggingUtils*',
        '**/logging/AccessCheckoutLogger*',
        // excludes classes generated by for coroutines by compiler, e.g. see method doGet in HttpsClient
        '**/HttpsClient*$2*',
        '**/*.<init>(*DefaultConstructorMarker*)'
]

def classDirs = files([
        fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter) +
        fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
])

def execData = files("$buildDir/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec")

def sourceDirs = files(["${project.projectDir}/src/main/java"])

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    executionData.setFrom(execData)
    classDirectories.setFrom(classDirs)
    sourceDirectories.setFrom(sourceDirs)

    reports {
        html.enabled true
    }
}

task jacocoTestCoverageVerification(type: JacocoCoverageVerification, dependsOn: 'jacocoTestReport') {
    executionData.setFrom(execData)
    classDirectories.setFrom(classDirs)
    sourceDirectories.setFrom(sourceDirs)

    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }
        }
    }
}
