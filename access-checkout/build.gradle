apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

apply from: './gradle/android.gradle'
apply from: './gradle/jacoco.gradle'
apply from: './gradle/upload.gradle'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:${versions['appcompat']}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${versions['kotlin']}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions['kotlinx-coroutines']}"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:${versions['localbroadcastmanager']}"

    androidTestImplementation "androidx.test.ext:junit:${versions['androidx.test.ext:junit']}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions['espresso']}"

    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:${versions['mockito-kotlin']}"
    androidTestImplementation "org.mockito:mockito-android:${versions['mockito-android']}"

    androidTestImplementation("com.github.tomakehurst:wiremock:${versions['com.github.tomakehurst:wiremock']}") {
        //Allows us to use the Android version of Apache httpclient instead
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'

        //Resolves the Duplicate Class Exception
        //duplicate entry: org/objectweb/asm/AnnotationVisitor.class
        exclude group: 'asm', module: 'asm'

        //Fixes Warning conflict with Android's version of org.json
        //org.json:json:20090211 is ignored for debugAndroidTest as it may be conflicting with the internal version provided by Android.
        exclude group: 'org.json', module: 'json'
    }

    androidTestImplementation "org.apache.httpcomponents:httpclient-android:${versions['httpclient-android']}"
    androidTestImplementation "org.awaitility:awaitility-kotlin:${versions['awaitility-kotlin']}"
    androidTestImplementation "com.google.android.gms:play-services-vision:${versions['play-services-vision']}"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions['kotlinx-coroutines']}"
    androidTestImplementation "androidx.lifecycle:lifecycle-runtime-testing:${versions['lifecycle-runtime-testing']}"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:${versions['kotlin']}"
    // added to resolve an exception where the addition of the 'lifecycle-runtime-testing' library
    // provokes a Duplicate class exception when building - See https://stackoverflow.com/questions/56639529
    androidTestImplementation "com.google.guava:listenablefuture:${versions['listenablefuture']}"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions['kotlinx-coroutines']}"
    testImplementation "org.jetbrains.kotlin:kotlin-test:${versions['kotlin']}"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${versions['mockito-kotlin']}"
    testImplementation "androidx.test.ext:junit:${versions['androidx.test.ext:junit']}"
    testImplementation "org.awaitility:awaitility-kotlin:${versions['awaitility-kotlin']}"
    testImplementation "junit:junit:${versions['junit']}"

    // override the version of bcprov-jdk15on to avoid the ssl internal errors
    testImplementation("org.robolectric:robolectric:${versions['robolectric']}") {
        //Allows us to use the Android version of Apache httpclient instead
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    testImplementation "com.squareup.okhttp3:mockwebserver:${versions['mockwebserver']}"
    testImplementation "au.com.dius:pact-jvm-consumer-junit:${versions['pact-jvm-consumer-junit']}"


    testImplementation("org.wiremock:wiremock:${versions['org.wiremock:wiremock']}") {
        //Allows us to use the Android version of Apache httpclient instead
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

}

tasks.withType(Test).configureEach {
    def forks = Math.max((int) (Runtime.getRuntime().availableProcessors() / 2), 1)
    maxParallelForks = forks
    forkEvery = 50
    println("Setting maxParallelForks to $forks on test task ${it.name}")
}
