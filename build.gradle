buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.5.0'
        classpath 'org.jacoco:org.jacoco.core:0.8.7'
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.1.0'
    }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'org.jlleitschuh.gradle.ktlint-idea'

    ktlint {
        android.set(true)
        outputColorName.set('RED')
        disabledRules = ['max-line-length']
    }

    if (project.property('isLibrary') == "true") {
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-parcelize'

        apply from: './gradle/android.gradle'
        apply from: './gradle/jacoco.gradle'
        apply from: './gradle/javadoc.gradle'
        apply from: './gradle/upload.gradle'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
