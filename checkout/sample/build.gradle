apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.worldpay.access.checkout"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "api", "merchant"
    productFlavors {
        mock {
            dimension = "api"
            applicationIdSuffix = ".mock"
        }

        prod {
            dimension = "api"
        }

        worldpay {
            dimension = "merchant"
        }
    }

    applicationVariants.all { variant ->
        variant.buildConfigField "String", "MERCHANT_ID", merchantID
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    implementation project(':access-checkout-android')

    mockImplementation("com.github.tomakehurst:wiremock:2.22.0") {
        //Allows us to use the Android version of Apache httpclient instead
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'

        //Resolves the Duplicate Class Exception
        //duplicate entry: org/objectweb/asm/AnnotationVisitor.class
        exclude group: 'asm', module: 'asm'

        //Fixes Warning conflict with Android's version of org.json
        //org.json:json:20090211 is ignored for debugAndroidTest as it may be conflicting with the internal version provided by Android.
        exclude group: 'org.json', module: 'json'
    }
    mockImplementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    androidTestMockImplementation 'com.squareup.okhttp3:okhttp:3.12.0'
    androidTestMockImplementation 'org.awaitility:awaitility-kotlin:3.1.6'

}

repositories {
    mavenCentral()
}

